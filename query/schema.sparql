PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

CONSTRUCT {
  ?predicate ?a ?b ; a rdf:Property .
  ?concept   ?c ?d ; a rdfs:Class .
} 
FROM NAMED <http://example.org/contacts_b>
FROM NAMED <http://example.org/contacts_a>
FROM NAMED <http://xmlns.com/foaf/0.1/>
FROM NAMED <http://schema.org/>
WHERE {
  {
    SELECT DISTINCT ?predicate {
      { SELECT DISTINCT ?x {GRAPH <http://example.org/contacts_a> {[] ?x []}} }
      UNION
      { SELECT DISTINCT ?x {GRAPH <http://example.org/contacts_b> {[] ?x []}} } 
      GRAPH ?g {?x (owl:equivalentProperty|^owl:equivalentProperty|owl:sameAs|^owl:sameAs|rdfs:subPropertyOf)* ?predicate}
    }
  }
  {
  	{ SELECT DISTINCT ?y {GRAPH <http://example.org/contacts_a> {[] a ?y}} }
  	UNION
  	{ SELECT DISTINCT ?y {GRAPH <http://example.org/contacts_b> {[] a ?y}} } 
  } 
  UNION 
  { GRAPH ?h {?predicate (rdfs:range|rdfs:domain) ?y} }
  GRAPH ?i {?y (rdfs:subClassOf|owl:sameAs|^owl:sameAs|owl:equivalentClass|^owl:equivalentClass)* ?concept}
  OPTIONAL { GRAPH ?j {?predicate ?a ?b} }
  OPTIONAL { GRAPH ?k {?concept ?c ?d} }
} 