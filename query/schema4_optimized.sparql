PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

CONSTRUCT {
  ?predicate ?a ?b ; a rdf:Property .
  ?concept   ?c ?d ; a rdfs:Class .
} 
FROM <http://example.org/contacts_b>
FROM <http://example.org/contacts_a>
WHERE {
  {
    SELECT DISTINCT ?predicate {
      { SELECT DISTINCT ?x {[] ?x []} }.
      {?x (rdfs:subPropertyOf|owl:equivalentProperty|^owl:equivalentProperty|owl:sameAs|^owl:sameAs)* ?predicate FILTER(!isBlank(?predicate))}
      UNION { SERVICE <http://localhost:3030/terminology/sparql> {GRAPH ?g {?x (rdfs:subPropertyOf|owl:equivalentProperty|^owl:equivalentProperty|owl:sameAs|^owl:sameAs)* ?predicate FILTER(!isBlank(?predicate))}}}
    }
  }
  
  { SELECT DISTINCT ?y {[] a ?y} } 
  UNION { 	    
    {?predicate (rdfs:range|rdfs:domain) ?y}
    UNION { SERVICE <http://localhost:3030/terminology/sparql> {GRAPH ?h {?predicate (rdfs:range|rdfs:domain) ?y} }}
  }

  {?y (rdfs:subClassOf|owl:sameAs|^owl:sameAs|owl:equivalentClass|^owl:equivalentClass)* ?concept FILTER(!isBlank(?concept))}
	UNION { SERVICE <http://localhost:3030/terminology/sparql> {GRAPH ?i {?y (rdfs:subClassOf|owl:sameAs|^owl:sameAs|owl:equivalentClass|^owl:equivalentClass)* ?concept FILTER(!isBlank(?concept))}}}
    
  OPTIONAL { 
    {?predicate ?a ?b} 
    UNION {SERVICE <http://localhost:3030/terminology/sparql> {GRAPH ?j {?predicate ?a ?b}}}
  }
  OPTIONAL { 
    {?concept ?c ?d}
    UNION {SERVICE <http://localhost:3030/terminology/sparql> {GRAPH ?k {?concept ?c ?d}}}
  }
}
